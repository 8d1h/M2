--- status: Draft
--- author(s): Gregory G. Smith
--- notes: 

document { 
     Key => {isMonomialIdeal, (isMonomialIdeal,Ideal), (isMonomialIdeal,Thing), (isMonomialIdeal,MonomialIdeal)},
     Headline => "whether something is a monomial ideal",
     Usage => "isMonomialIdeal I",
     Inputs => {
	  "I" => Thing => ""
	  },
     Outputs => {
	  {TO "true", " if ", TT "I", " is a ", TO2("MonomialIdeal", "monomial ideal"), " or if ", TT "I", 
	       " is an ", TO2("Ideal", "ideal"), " generated by monomials; otherwise ", TO "false"}
	  },
     EXAMPLE {
	  "QQ[w,x,y,z];",
	  "I = minors(2, matrix{{w,x,y},{x,y,z}})",
	  "isMonomialIdeal I",
	  "J = ideal leadTerm I",
	  "isMonomialIdeal J",
	  "K = monomialIdeal I",
	  "isMonomialIdeal K"
	  },
     }

