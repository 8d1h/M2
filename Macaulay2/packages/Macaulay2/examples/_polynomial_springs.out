--warning: checksum has changed for a map
--package "Macaulay2Core" loaded
--package "PrimaryDecomposition" loaded
--loading the Macaulay2 documentation
--package "Macaulay2" loaded

i1 : R = QQ[x,y,z];

i2 : R

o2 = R

o2 : PolynomialRing

i3 : describe R

o3 = QQ [x, y, z]

i4 : 0_R

o4 = 0

o4 : R

i5 : 1_R

o5 = 1

o5 : R

i6 : 11_R

o6 = 11

o6 : R

i7 : R_0^10+R_1^3+R_2

      10    3
o7 = x   + y  + z

o7 : R

i8 : numgens R

o8 = 3

i9 : apply(numgens R, i -> R_i^i)

             2
o9 = {1, y, z }

o9 : List

i10 : sum(numgens R, i -> R_i^i)

       2
o10 = z  + y + 1

o10 : R

i11 : gens R

o11 = {x, y, z}

o11 : List

i12 : vars R

o12 = | x y z |

              1       3
o12 : Matrix R  <--- R

i13 : index x, index y, index z

o13 = (0, 1, 2)

o13 : Sequence

i14 : coefficientRing R

o14 = QQ

o14 : Ring

 -- the class of all rational numbers

i15 : random(2,R)

      5             8       2  2
o15 = -*x*y + x*z - -*y*z - -*z
      2             9       5

o15 : R

i16 : basis(2,R)

o16 = | x2 xy xz y2 yz z2 |

              1       6
o16 : Matrix R  <--- R

i17 : ZZ[a,b,c][d,e,f];

i18 : (a+d+1)^2

       2                2
o18 = d  + (2a + 2)d + a  + 2a + 1

o18 : (ZZ [a, b, c])[d, e, f]

i19 : QQ[rho,sigma,tau];

i20 : (rho - sigma)^2

         2                     2
o20 = rho  - 2rho*sigma + sigma

o20 : QQ [rho, sigma, tau]

i21 : ZZ[b..k];

i22 : ZZ[symbol b .. symbol k];

i23 : vars (0..4)

o23 = (a, b, c, d, e)

o23 : Sequence

i24 : ZZ[vars (0..4),vars(26..30),vars 51]

o24 = ZZ [a, b, c, d, e, A, B, C, D, E, Z]

o24 : PolynomialRing

i25 : ZZ[t,p_0,p_1,q_0,q_1];

i26 : ZZ[p_(0,0) .. p_(2,1),q_0..q_5]

o26 = ZZ [p   , p   , p   , p   , p   , p   , q , q , q , q , q , q ]
           0,0   0,1   1,0   1,1   2,0   2,1   0   1   2   3   4   5

o26 : PolynomialRing

i27 : (p_(0,0)+q_2-1)^2

       2                 2
o27 = p    + 2p   *q  + q  - 2p    - 2q  + 1
       0,0     0,0  2    2     0,0     2

o27 : ZZ [p   , p   , p   , p   , p   , p   , q , q , q , q , q , q ]
           0,0   0,1   1,0   1,1   2,0   2,1   0   1   2   3   4   5

i28 : protect xx; protect yy; protect zz;

i31 : ZZ[ee_[xx],ee_[yy],ee_[zz]]

o31 = ZZ [ee    , ee    , ee    ]
            [xx]    [yy]    [zz]

o31 : PolynomialRing

i32 : ZZ[a,b,c] === ZZ[a,b,c]

o32 = false

o32 : Boolean

i33 : R = QQ[a..d];

i34 : a^127

       127
o34 = a

o34 : R

i35 : S = QQ[a..d,MonomialSize=>16];

i36 : a^32767

       32767
o36 = a

o36 : S

i37 : 
