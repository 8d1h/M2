newPackage ("TangentCone"
       	Version => "1.0", 
    	Date => "June 27, 2006",
    	Authors => {
	     {Name => "Craig Huneke", Email => "huneke@math.ku.edu"},
	     {Name => "David Eisenbud", Email => "de@msri.org"}
	     },
    	Headline => "tangent cones",
    	DebuggingMode => true
    	)

tangentCone = (stashValue symbol tangentCone) ((i) -> (
     --given an (in)homogeneous ideal i in a polynomial ring S over a field,
     --whose variables are all of degree 1,
     --returns a minimal generating set for the ideal of the
     --tangent cone (at the point defined by the ideal of variables)
     -- of the scheme defined by i -- that is, the
     --ideal that defines gr(S/I), the ideal generated by the lowest degree forms
     --of all the polynomials in i.
     --This follows the method in DE's book, Ch 15.
     R := ring i;
     n := numgens R;
     --make a ring SS with one more variable, in it's own block.
     SS := (coefficientRing R)[Variables=>n+1,MonomialOrder=>{1,n}];
     toSS := map(SS, R, (vars SS)_{1..n});
     fromSS := map(R, SS, 1_R|vars R);
     iSS := toSS i;
     I := homogenize(iSS, SS_0);
     inI := ideal leadTerm(1, gens gb I);
     J := fromSS inI;
     ideal mingens J))

localTangentCone = (stashValue symbol localTangentCone) ((i) -> (
     --given an (in)homogeneous ideal i in a polynomial ring S over a field,
     --whose variables are all of degree 1,
     --returns a minimal generating set for the ideal of the
     --tangent cone (at the point defined by the ideal of variables)
     -- of the scheme defined by i -- that is, the
     --ideal that defines gr(S/I), the ideal generated by the lowest degree forms
     --of all the polynomials in i.
     --This follows the method in DE's book, Ch 15.
     R := ring i;
     n := numgens R;
     --make a ring SS with local ordering
     SS := (coefficientRing R) [Variables=>n,MonomialOrder=>Weights=>splice{n:-1}];
     toSS = map(SS, R, vars SS);
     fromSS = map(R, SS, vars R);
     iSS := toSS i;
     inI := ideal leadTerm(1, gens gb iSS);
     J := fromSS inI;
     ideal mingens J))

beginDocumentation()

end

restart
loadPackage "TangentCone"
kk = ZZ/101
kk = QQ

S=kk[x,y,z]
i=ideal"xz-y3,yz-x4,z2-x3y2"
tangentCone i						    -- | z2 yz xz y4 |
localTangentCone i

i=ideal"z2-x5,zx-y3"
tangentCone i						    -- | z2 xz y3z y6 |
localTangentCone i

--And an example where the degrees don't go up by at least 2 each  
S=kk[a..c]
i=ideal"a3+a2c2,a2b+ac3+c5"
tangentCone i				   -- | a2b a3 a2c3 2abc5-ac6 ac7 bc9 |
betti tangentCone i
localTangentCone i

S=kk[a..c, MonomialOrder=>Weights=>splice{3:-1}]
i=ideal"a3+a2c2,a2b+ac3+c5"
gens gb i
leadTerm(1,oo)


end


-- 
-- 
-- 
-- -----
-- David Eisenbud
-- Director, Mathematical Sciences Research Institute
-- 17 Gauss Way, Berkeley CA 94720
-- www.msri.org
-- 510-642-0143
-- 
-- 
-- 
-- 

