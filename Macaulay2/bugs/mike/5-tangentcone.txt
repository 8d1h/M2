--From: David Eisenbud <de@msri.org>
--Date: June 24, 2006 9:59:22 PM EDT
--To: Craig Huneke <huneke@math.ku.edu>, Michael Stillman <mike@math.cornell.edu>, Dan Grayson <dan@math.uiuc.edu>
--Subject: slightly cleaned up tangentCone

tangentCone = i ->(
     --given an (in)homogeneous ideal i in a polynomial ring S over a field,
     --whose variables are all of degree 1,
     --returns a minimal generating set for the ideal of the
     --tangent cone (at the point defined by the ideal of variables)
     -- of the scheme defined by i -- that is, the
     --ideal that defines gr(S/I), the ideal generated by the lowest degree forms
     --of all the polynomials in i.
     --This follows the method in DE's book, Ch 15.
     n:=# gens ring i;
     --make a ring SS with one more variable, in it's own block.
     SS:=(coefficientRing ring i)[vars(0..n),MonomialOrder=>{GRevLex=>1,GRevLex=>n}];
     toSS:=map(SS, ring i, (vars SS)_{1..n});
     fromSS:=map(ring i, SS, matrix{{0_(ring i)}}|vars ring i);
     iSS := toSS i;
     I:=homogenize(iSS, SS_0);
     m:=coefficients(gens gb I, Variables=>{SS_0});
     N:=(m_{1})_0; -- this is a matrix, one column for each gb element, of the forms of different degrees in the new var.
     J:= ideal apply(rank source N, i->(
       j:=0;
       while (N_i)_j ==0 do j=j+1;
       (N_i)_j)); -- this picks the top nonzero element in each column
                -- that is, the lowest degree form in the old vars
    mingens fromSS J)



tangentCone = (i) ->(
     --given an (in)homogeneous ideal i in a polynomial ring S over a field,
     --whose variables are all of degree 1,
     --returns a minimal generating set for the ideal of the
     --tangent cone (at the point defined by the ideal of variables)
     -- of the scheme defined by i -- that is, the
     --ideal that defines gr(S/I), the ideal generated by the lowest degree forms
     --of all the polynomials in i.
     --This follows the method in DE's book, Ch 15.
     R := ring i;
     n := numgens R;
     --make a ring SS with one more variable, in it's own block.
     SS := (coefficientRing R)[Variables=>n+1,MonomialOrder=>{1,n}];
     toSS = map(SS, R, (vars SS)_{1..n});
     fromSS = map(R, SS, 1_R|vars R);
     iSS := toSS i;
     I = homogenize(iSS, SS_0);
     inI := ideal leadTerm(1, gens gb I);
     J := fromSS inI;
     ideal mingens J)

localTangentCone = (i) ->(
     --given an (in)homogeneous ideal i in a polynomial ring S over a field,
     --whose variables are all of degree 1,
     --returns a minimal generating set for the ideal of the
     --tangent cone (at the point defined by the ideal of variables)
     -- of the scheme defined by i -- that is, the
     --ideal that defines gr(S/I), the ideal generated by the lowest degree forms
     --of all the polynomials in i.
     --This follows the method in DE's book, Ch 15.
     R := ring i;
     n := numgens R;
     --make a ring SS with local ordering
     SS := (coefficientRing R)[Variables=>n,MonomialOrder=>Weights=>splice{n:-1}];
     toSS = map(SS, R, vars SS);
     fromSS = map(R, SS, vars R);
     iSS := toSS i;
     inI := ideal leadTerm(1, gens gb iSS);
     J := fromSS inI;
     ideal mingens J)
end

restart
load "/Users/mike/M2/Macaulay2/bugs/mike/5-tangentcone.txt"
kk = ZZ/101
kk = QQ
S=kk[x,y,z]
i=ideal"xz-y3,yz-x4,z2-x3y2"
tangentCone i
localTangentCone i
i=ideal"z2-x5,zx-y3"
tangentCone i
localTangentCone i
--And an example where the degrees don't go up by at least 2 each time
S=kk[a..c]
i=ideal"a3+a2c2,a2b+ac3+c5"
betti tangentCone i
tangentCone i
localTangentCone i
S=kk[a..c, MonomialOrder=>Weights=>splice{3:-1}]
i=ideal"a3+a2c2,a2b+ac3+c5"
gens gb i
leadTerm(1,oo)
